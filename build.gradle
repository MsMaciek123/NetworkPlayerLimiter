plugins {
    id 'java'
    id 'eclipse'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'pl.msmaciek'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "panda-repo"
        url = "https://repo.panda-lang.org/releases"
    }
}

dependencies {
    compileOnly("com.velocitypowered:velocity-api:3.1.1")
    annotationProcessor("com.velocitypowered:velocity-api:3.1.1")

    compileOnly("org.projectlombok:lombok:1.18.34")
    annotationProcessor("org.projectlombok:lombok:1.18.34")

    implementation("dev.rollczi:litecommands-velocity:3.9.2")
    implementation("de.exlll:configlib-yaml:4.5.0")
    implementation("net.kyori:adventure-text-minimessage:4.17.0")
}

tasks {
    shadowJar {
        archiveClassifier.set("")

        relocate("dev.rollczi.litecommands", "pl.msmaciek.networkplayerlimiter.libs.litecommands")
        relocate("de.exlll.configlib", "pl.msmaciek.networkplayerlimiter.libs.configlib")
        relocate("net.kyori.adventure.text.minimessage", "pl.msmaciek.networkplayerlimiter.libs.minimessage")
    }

    build {
        dependsOn(shadowJar)
    }
}

def targetJavaVersion = 21
java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = ['version': project.version]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

project.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
